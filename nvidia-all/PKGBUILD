# Maintainer : Cian Mc Govern <cian@cianmcgovern.com>
# Contributor : Ng Oon-Ee <ngoonee.talk@gmail.com>
# Based on nvidia-beta by Dan Vratil <vratil@progdansoft.com>

pkgname=nvidia-all
pkgver=319.32
pkgrel=2
pkgdesc="NVIDIA drivers for linux."
arch=('i686' 'x86_64')
[ "$CARCH" = x86_64 ] && ARCH=x86_64 &&_srcname=NVIDIA-Linux-x86_64-${pkgver}-no-compat32
[ "$CARCH" = "i686" ] && ARCH=x86 && _srcname=NVIDIA-Linux-x86-${pkgver}
provides=("nvidia=${pkgver}")
url="http://www.nvidia.com/"
depends=("nvidia-utils=${pkgver}")
makedepends=('linux-headers' 'kmod>=9-2')
conflicts=('nvidia-96xx' 'nvidia-71xx' 'nvidia-legacy' 'nvidia' 'nvidia-beta' 'nvidia-beta-all')
license=('custom')
install=nvidia.install
source=("http://us.download.nvidia.com/XFree86/Linux-$ARCH/${pkgver}/${_srcname}.run"
        "nvidia-linux-3.10.patch")

[ "$CARCH" = "i686" ] && md5sums=('978339ff37c3207f3a39d3952721dcdb')
[ "$CARCH" = "x86_64" ] && md5sums=('d181d856768868823df3bda4607a5315')

md5sums+=('547b74f49f81b22f214e2770e31d62b6')

KERNELS=`cat /usr/lib/modules/extramodules*/version`

build()
{
    sh ${_srcname}.run --extract-only
    cd ${_srcname}/kernel
    # Loop through all detected kernels
    for _kernver in $KERNELS;
    do
        cd ${srcdir}/${_srcname}
        cp -R kernel kernel-${_kernver}
        cd kernel-${_kernver}
        if [[ "$_kernver" == *3.10* ]];
        then
            echo Applying patch for Linux 3.10
            patch -p2 < ${srcdir}/nvidia-linux-3.10.patch
        fi
        echo Building module for $_kernver
        make SYSSRC=/usr/lib/modules/"${_kernver}"/build module
    done
}

package() {

    # Find all extramodules directories
    _EXTRAMODULES=`find /usr/lib/modules -name version | sed 's|\/usr\/lib\/modules\/||; s|\/version||'`

    # Loop through all detected kernels
    for _kernver in $KERNELS;
    do
        cd "${srcdir}/${_srcname}/kernel-${_kernver}"
        # Loop through all detected extramodules directories
        for _moduledirs in $_EXTRAMODULES
        do
            # Check which extramodules directory corresponds with the built module
            if [ `cat "/usr/lib/modules/${_moduledirs}/version"` = $_kernver ]; then
                mkdir -p "${pkgdir}/usr/lib/modules/${_moduledirs}/"
                install -m644 nvidia.ko "${pkgdir}/usr/lib/modules/${_moduledirs}/"
                gzip "${pkgdir}/usr/lib/modules/${_moduledirs}/nvidia.ko"
            fi
        done
    done

    install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia.conf"
}
